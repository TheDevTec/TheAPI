plugins {
    id 'java'
    id 'maven-publish'
}
def versionValue;
if (project.hasProperty('GIT_COMMIT')) {
	versionValue = System.getenv("GIT_COMMIT");
} else {
    versionValue='13.4';
}
group = 'me.devtec.theapi.loaders'
version = '13.4'

repositories {
	mavenCentral()
}

allprojects {
    repositories {
        maven {
            url = 'https://jitpack.io'
        }
    }
}
dependencies {
    implementation 'com.github.TheDevTec:TheAPI-Shared:13.4'
}
subprojects {
    apply plugin: 'java'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.24'
   		annotationProcessor 'org.projectlombok:lombok:1.18.24'
        implementation 'com.github.TheDevTec:TheAPI-Shared:13.4'
    }
    
    tasks.withType(JavaCompile) {
    	options.annotationProcessorPath = configurations.annotationProcessor
	}
	task buildJar(type: Jar) {
        from sourceSets.main.output
        archiveBaseName.set(project.name)
    	archiveVersion.set(versionValue)
    }
}

task buildJar(type: Jar) {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from subprojects.collect { it.sourceSets.main.output }
    archiveBaseName.set(project.name)
    archiveVersion.set(versionValue)
}

build.dependsOn buildJar

// Definice publikování do Maven Local
publishing {
    publications {
        mavenJava(MavenPublication) {
            // Odstranění standardní komponenty, pokud existuje
            // Zabrání duplikaci artefaktů
            artifacts.clear()

            // Přidání vlastního JAR souboru
            artifact tasks.buildJar.archiveFile.get()

            // Skupina, artefakt a verze
            groupId = 'me.devtec.theapi.loaders'
            artifactId = project.name
            version = versionValue
        }
    }
    repositories {
        mavenLocal() // Publikování do Maven Local
    }
}

// Zajištění, že `publishToMavenLocal` závisí na buildu
tasks.publishToMavenLocal {
    dependsOn buildJar
}