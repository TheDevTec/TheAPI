plugins {
	id 'java-library'
    id 'maven-publish'
}

def versionValue = System.getenv('VERSION') ?: '13.5.4'
group = 'me.devtec.theapi.loaders'
version = versionValue

repositories {
    mavenCentral()
    mavenLocal()
}

allprojects {
    repositories {
        maven {
            url = 'https://jitpack.io'
        }
    }
}

dependencies {
    implementation 'com.github.TheDevTec:TheAPI-Shared:13.5.4'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.36'
        annotationProcessor 'org.projectlombok:lombok:1.18.36'
        implementation 'com.github.TheDevTec:TheAPI-Shared:13.5.4'
    }

    tasks.withType(JavaCompile) {
        options.annotationProcessorPath = configurations.annotationProcessor
    }

    task buildJar(type: Jar) {
        from {
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
        from sourceSets.main.output
        archiveBaseName.set(project.name)
        archiveVersion.set(versionValue)
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
	            artifactId = project.name
	            version = versionValue
                from components.java
            }
        }
    }
}

task buildJar(type: Jar) {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from subprojects.collect { it.sourceSets.main.output }
    archiveBaseName.set(project.name)
    archiveVersion.set(versionValue)
}

publishing {
    publications {
        jar(MavenPublication) {
            from components.java
            artifactId = 'TheAPI'
            version = versionValue
            publishJarPublicationToMavenLocal.dependsOn buildJar

        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

build.dependsOn buildJar